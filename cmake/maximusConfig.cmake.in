if(NOT TARGET maximus::maximus)
    cmake_policy(PUSH) # Save project's policies
    if(POLICY CMP0074)
        cmake_policy(SET CMP0074 NEW)
    endif()
    include(CMakeFindDependencyMacro)

    # In order to prevent conflicts with similarly named modules, bundled modules should be found first
    set(CMAKE_MODULE_PATH ${CMAKE_CURRENT_LIST_DIR} ${CMAKE_MODULE_PATH})

    # Find Apache Arrow
    find_dependency(Arrow REQUIRED)
    find_dependency(ArrowAcero REQUIRED)
    find_dependency(Parquet REQUIRED)
    find_dependency(Taskflow REQUIRED)

    if ("@MAXIMUS_WITH_GPU@")
        find_dependency(CUDAToolkit REQUIRED)
        find_dependency(CUDF REQUIRED)
    endif()

    if ("@MAXIMUS_WITH_PROFILING@")
        find_dependency(caliper)
    endif ()

    # Clean-up module path.
    #
    list(REMOVE_ITEM CMAKE_MODULE_PATH ${CMAKE_CURRENT_LIST_DIR})

    include("${CMAKE_CURRENT_LIST_DIR}/maximusTargets.cmake")
    cmake_policy(POP) # Restore project's policies
endif()
